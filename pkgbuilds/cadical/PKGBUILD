# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=cadical
pkgver=1.5.2
pkgrel=1
pkgdesc="A simple CDCL satisfiability solver"
arch=('x86_64')
url="http://fmv.jku.at/cadical/"
license=('MIT')
source=("https://github.com/arminbiere/cadical/archive/rel-$pkgver.tar.gz")
sha256sums=('4a4251bf0191677ca8cda275cb7bf5e0cf074ae0056819642d5a7e5c1a952e6e')

build() {
  cd "$srcdir/$pkgname-rel-$pkgver"
  ./configure -fPIC
  make
}

package() {
  cd "$srcdir/$pkgname-rel-$pkgver"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd src
  install -Dm644 cadical.hpp  "$pkgdir/usr/include/cadical.hpp"
  install -Dm644 ccadical.h  "$pkgdir/usr/include/ccadical.h"
  cd ..
  cd build
  install -Dm755 cadical      "$pkgdir/usr/bin/cadical"
  install -Dm755 libcadical.a "$pkgdir/usr/lib/libcadical.a"
}

